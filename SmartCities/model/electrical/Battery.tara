dsl Tafat
use PowerGrid

Behavior Electrical &000.001
	sub BatteryElectrical
		var measure:Power activePower = 0 W
		var measure:Energy capacity
		var measure:Percentage stateOfCharge
		var word mode > Idle*; Charging; Discharging
		var measure:Power maxDischargePower
		var measure:Power maxChargePower
		var measure:Percentage transferLoses = 20 %

		sub Mode // BatteryFullBehaviorBasic
			on Battery
				ConditionalAction("() -> stateOfCharge() > 95 && mode() == Mode.Charging", "() -> mode(Mode.Idle)")
				ConditionalAction("() -> stateOfCharge() > 20 && mode() == Mode.Discharging", "() -> mode(Mode.Idle)")

		sub Power

			sub BasicPowerControl // BatteryFullBehaviorChargeDischargeBasic
				on Battery
					ConditionalAction("() -> mode() == Mode.Idle", "() -> activePower(0)")
					ConditionalAction("() -> mode() == Mode.Charging", "() -> activePower(maxChargePower())")
					ConditionalAction("() -> mode() == Mode.Discharging", "() -> activePower(maxDischargePower())")

			sub TimeBasedPowerControl
				on Battery
					start = ("() -> {
						double wattsPerSecondCharging = ((capacity() / timeToCharge()) * (1 + transferLoses())) * 1000
						wattsPerSecondCharging(wattsPerSecondCharging < maxChargePower() ? wattsPerSecondCharging : maxChargePower)
						double wattsPerSecondDischarging = capacity / timeToDischarge * 1000
						wattsPerSecondDischarging(wattsPerSecondDischarging < maxChargePower() ? wattsPerSecondDischarging : maxChargePower)
					}")
					var measure:Time timeToCharge
					var measure:Time timeToDischarge
					var measure:Power wattsPerSecondCharging
					var measure:Power wattsPerSecondDischarging
					ConditionalAction("() -> mode() == Mode.Idle", "() -> activePower(0)")
					ConditionalAction("() -> mode() == Mode.Charging", "() -> activePower(wattsPerSecondCharging())")
					ConditionalAction("() -> mode() == Mode.Discharging", "() -> activePower(wattsPerSecondDischarging())")

			sub CurvePowerControl // BatteryFullBehaviorChargeDischargeCurve
				on Battery
					PointSet Charge > Point(0.0, 0.7992); Point(0.8, 1.0); Point(1.0, 0.3846)
					PointSet Discharge > Point(0.0, 0.7272); Point(0.8, 0.8181); Point(1.0, 1.0)
					TableFunction(Charge) ChargeCurve > LinearInterpolation; LinearExtrapolation
					TableFunction(Discharge) DischargeCurve > LinearInterpolation; NoneExtrapolation
					ConditionalAction("() -> mode() == Mode.Idle", "() -> activePower(0)")
					ConditionalAction("() -> mode() == Mode.Charging", "() -> activePower(chargeCurve().y(stateOfCharge))")
					ConditionalAction("() -> mode() == Mode.Discharging", "() -> activePower(dischargeCurve().y(stateOfCharge))")