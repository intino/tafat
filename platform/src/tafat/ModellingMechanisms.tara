dsl Proteo

Concept PeriodicActivity is abstract
	var function:Execute execute is final

Concept Action extends PeriodicActivity is feature
	var function:Action action
	var function:Execute execute = 'self.action()' is final


Concept ConditionalAction extends PeriodicActivity is feature
	var boolean condition = true is final reactive
	var function:Action action
	var function:Execute execute = 'if(self.condition()) self.action();' is final

Concept EquationSystem is component
	var word:{Euler} solver = Euler
	var double step = 0.1
	Concept Element is feature
		var function:Function calculus
		var double value = 0 is terminal
		sub Stock:{0..1}
		sub Flow:{0..1}
	Concept Param
		var double value is reactive

Concept StateChart extends PeriodicActivity is feature
	var function:Execute execute = @updateStateChart is final
	var function:Message receiveMessage = @receiveMessage is final
	var string message = "" is private
	var StateChart current = empty is private
	Concept State extends StateChart
		Concept Action
			var function:Action action is final
			sub EntryAction
			sub ExitAction
			sub PeriodicAction
	Concept Transition
		var State from is final
		var State to is final
		var function:Action action = ''
		Concept:{1..1} Trigger
			var function:CheckTransition check
			sub Condition >var function:CheckTransition check
			sub TimeBased
				var date when = "" is private
				var function:CheckTransition check = 'return !self.when().isAfter(getDateTime());' is private
				var function:Action activate is private
				sub Timeout
					var function:Action activate = 'self.when(getDateTime().plusSeconds(self.timeout()));'
					var function:Timeout timeout is final
				sub After
					var function:Action activate = 'self.when(getDateTime().plusSeconds(self.fixedTime()));'
					var integer:Time fixedTime is final
				sub Rate
					var function:Action activate = 'self.when(getDateTime().plusSeconds(self.unit() / self.times()));'
					var integer:{times} times
					var integer unit
			sub Message
				var string expectedMessage
				var function:CheckTransition check = 'return self.ownerAs(StateChart.class).message().equals(self.expectedMessage());'

Concept Task is feature
	var word[]:DayOfWeek days = Monday Tuesday Wednesday Thursday Friday Saturday Sunday
	var date scheduledDate = "" is private
	var boolean check = 'return !self.scheduledDate().isAfter(getDateTime());' is final reactive private
	var function:Action program = @programTask is final private
	Concept:{1..1} Start
		var time start is final
		var integer:Time deviation = 0 minute
	Concept:{0..1} Finish
		sub End
			var time end is final
			var integer:Time deviation = 0 minute
		sub Duration
			var integer:Time duration is final
			var integer:Time deviation = 0 minute
	has Task
	has JobAction
	Concept Action
		var function:Action action is final
		sub StartAction
		sub RecurrentAction
		sub EndAction

Concept JobAction is feature
	var Job[] job is final
	sub StartJob
	sub RecurrentJob
	sub EndJob

Concept Job is feature
	Concept:{0..1} Start
		var integer:Time start is final
		var integer:Time deviation = 0 minute
	Concept:{0..1} Duration
		var integer:Time duration is final
		var integer:Time deviation = 0 minute
	has Job
	has JobAction
	Concept Action
		var function:Action action
		sub StartAction
		sub RecurrentAction
		sub EndAction

Concept TableFunction is feature
	Concept:{0..1} Interpolation
		sub NoneInterpolation
		sub LinearInterpolation
		sub PolynomialInterpolation
		sub SplineInterpolation
		sub StepInterpolation
	Concept:{0..1} Extrapolation
		sub NoneExtrapolation
		sub CustomExtrapolation > var function:Function extrapolationFunction
		sub NearestPointExtrapolation
		sub RepeatSeriesExtrapolation
		sub LinearExtrapolation
		sub PolynomialExtrapolation
		sub SplineExtrapolation
		sub StepExtrapolation
	Concept Data
		var double[] input
		var double output
	var function:Calculate get = '0' is terminal

Concept Map
	var function:Get get = '0' is final
	Concept Entry >var string key;var double value

Concept Rule
	var boolean check is reactive
