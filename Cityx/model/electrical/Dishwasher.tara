dsl Tafat
use PowerGrid


Behavior ElectricalConsumer
	on Dishwasher
		init = 'cr(capacity() <= 9 ? 0.45 + 0.09 * capacity() : 1.35 + 0.025 * capacity())'
		var measure:Power activePower = 0 W
		var double cr is final
		var integer:set capacity = 12 sets is final
		var word:[Off OnEco OnNormal OnIntensive] mode = Off
		var word:[AAAA AAA AA A B C D E F G] labelling = A is final
		Map Heat > Entry("OnEco", 1300 W); Entry("OnNormal", 1700 W); Entry("OnIntensive", 2000 W)
		Map HeatMinutes > Entry("OnEco", 15 m); Entry("OnNormal", 20 m); Entry("OnIntensive", 24 m)
		Map Wash > Entry("OnEco", 50 W); Entry("OnNormal", 70 W); Entry("OnIntensive", 90 W)
		Map WashMinutes > Entry("OnEco", 54 m); Entry("OnNormal", 50 m); Entry("OnIntensive", 50 m)
		Map Drying > Entry("OnEco", 1300 W); Entry("OnNormal", 1700 W); Entry("OnIntensive", 2000 W)
		Map DryingMinutes > Entry("OnEco", 15 m); Entry("OnNormal", 20 m); Entry("OnIntensive", 20 m)
		Map LabellingEfficiency > Entry("AAAA", 0.36); Entry("AAA", 0.41); Entry("AA", 0.47); Entry("A", 0.52)
			Entry("B", 0.58); Entry("C", 0.68); Entry("D", 0.78); Entry("E", 0.88); Entry("F", 0.98); Entry("G", 1.08)
		StateChart
			State Off > EntryAction('mode(Mode.Off); activePower(0)')
			State On
				State HeatWater > EntryAction('activePower(heat().get(mode) * labellingEfficiency().get(labelling()) * cr())')
				State WashRinse > EntryAction('wash().get(mode) * labellingEfficiency().get(labelling()) * cr()')
				State Drying > EntryAction('drying().get(mode) * labellingEfficiency().get(labelling()) * cr()')
				Transition(HeatWater, WashRinse) > Timeout('heatMinutes().get(mode())')
				Transition(WashRinse, Drying) > Timeout('washMinutes().get(mode())')
				Transition(Drying, Off) > Timeout('dryingMinutes().get(mode())')
			Transition(Off, On) > Condition('mode() != Mode.Off')