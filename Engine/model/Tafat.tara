dsl Proteo 

Concept Simulation is required single terminal > var date from; var date to

Concept Agent

Concept Entity
	has Entity
	Concept Feature into feature

Concept Facet into facet
	sub Behavior
	 	var integer step = 1 is terminal
	 	var native:OnStart onStart

		has Action
		has EquationSystem
		has StateChart
		has PointSet
		has Task
		has Job
	sub Aspect

Concept Action into property
	var native:Condition condition = empty
	var native:Action action

Concept Task
	var word... days > Monday*; Tuesday*; Wednesday*; Thursday*; Friday*; Saturday*; Sunday*
	Concept Start is required single
		var date time
		var measure:Time deviation = 0
	Concept Finish is single
		sub End
			var instant time
			var measure:Time deviation = 0
		sub Duration
			var measure:Time time
			var measure:Time deviation = 0
	has Task
	has JobAction

Concept Job
	Concept Start is single
		var measure:Time time
		var measure:Time deviation = 0
	Concept Duration is single
		var measure:Time time
		var measure:Time deviation = 0
	has Job
	has JobAction
	Concept Action
		var native:Action action
		sub StartAction
		sub RecurrentAction
		sub EndAction

Concept JobAction is terminal
	var Job... job
	sub StartJob
	sub RecurrentJob
	sub EndJob

Concept EquationSystem into property
	var word solver > Euler*
	var double step = 0.1
	Concept Element
		var native:Calculation calculation
		sub Stock
		sub Flow

Concept StateChart into property
	Concept State
		Concept Action
			var native:Action action
			sub EntryAction
			sub ExitAction
		has State
		has Transition
	Concept Transition
		var State from
		var State to
		var native:Action action = empty
		Concept Condition
			sub Boolean > var native:Condition condition
			sub Timeout > var native:Timeout timeout
			sub After > var measure:Time time
			sub Rate > var integer:time times; var word unit > Second; Minute; Hour; Day; Month; Year
			sub Message > var string message

Concept TableFunction
	var PointSet pointSet
	Concept Interpolation is single
		sub NoneInterpolation
		sub LinearInterpolation
		sub PolynomialInterpolation
		sub SplineInterpolation
		sub StepInterpolation
	Concept Extrapolation is single
		sub NoneExtrapolation
		sub CustomExtrapolation > var double value
		sub NearestPointExtrapolation
		sub RepeatSerieExtrapolation
		sub LinearExtrapolation
		sub PolynomialExtrapolation
		sub SplineExtrapolation
		sub StepExtrapolation

Concept PointSet into property
	Concept Definition
		sub Point > var double x; var double y
		sub X is single > var double... values
		sub Y is single > var double... values



Concept Calculator is terminal > var native:Calculation calculation

Concept OnStart is terminal > var native:Custom custom

Concept Map is terminal
	Concept Item > var string key; var double value

