dsl Proteo

use ModellingMechanisms
use Output

Concept:{1..1} Simulation
	var date from is final
	var date to is final
	has:{0..1} Output

MetaConcept Agent
	has Knol

MetaConcept Entity
	has Entity
	Concept Feature into feature

MetaFacet Aspect > on Entity

MetaFacet Behavior
	on Entity
	on Agent
	var integer step = 1 is terminal
	var integer timeout = 0
	var function:Check checkStep =
	--
	if ($.timeout() == 0) {
		$.timeout($.step() - 1);
		return true;
	}
	$.timeout($.timeout() - 1);
	return false;
	-- is final
	MetaConcept Start is final into feature prototype
		var function:Action start is final
	MetaConcept Knol is final into prototype feature
		has Knol
	has Periodic
	has Action is final
	has ConditionalAction is final
	has EquationSystem is final
	has StateChart is final
	has Task is final
	has TableFunction is final
	has PointSet is final
	has Job is final
	has Map is final

MetaFacet FmuModel > on Entity
	var string fmuPath is terminal
	var double step = 0.01 is terminal
	Concept Input
		var string fmuVariableName
		var function:Write write
	Concept Output
		var string fmuVariableName
		var function:Read read

MetaFacet ExternalModel > on Entity
	var function:Action doStep

MetaConcept Knol
	has Knol

MetaConcept View
	sub HeatMap
		var string path
		Concept Region
			var double[2] coordinates
			var function:Function calculate

Facet Parallelizable > on Behavior